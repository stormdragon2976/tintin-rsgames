#ACTION {^+CONFIG-READ config.ini|twitter_key:%*}
{
	#if {&{game.message}}
	{
		POST ${game.message}
	}
}
{9}

#ACTION {^+MSG This is your %1 win!$}
{
	#if {!&{game.message}}
	{
		#regexp {${game.name}} {{.*} {.*}} {#variable {game.message} {I won a game of ${game.name}!}};
		#else
		{
			#variable {game.message} {I won a game of #${game.name}!}
		}
	};
	#if {"${game.name}" != "Shut The Box"}
	{
		#delay {1} {POST ${game.message} This is my %1 win!;#unvariable {game.message}}
	}
}
{9}

#ACTION {^+MSG Tweet: %1}
{
	#variable {game.message} {%1}
}
{9}

#ACTION {^+MSG You have reached a score of %d. You win the game! This is your %2 win!$}
{
	#regexp {${game.name}} {{.*} {.*}} {POST I won ${game.name} with a final score of %1 points. This is my %2 win!};
	#else
	{
		POST I won #${game.name} with a final score of %1 points. This is my %2 win!
	}
}
{9}

#ACTION {^+MSG You win the game! You had a score of %d points!%2}
{
	#regexp {${game.name}} {{.*} {.*}} {#variable {game.message} {I won a game of ${game.name} with a final score of %1 points.}};
	#else
	{
		#variable {game.message} {I won a game of #${game.name} with a final score of %1 points.}
	}
}
{9}

#ACTION {^+MSG You win the game, with a total of %d cards! This is your %2 win!%*}
{
	POST I won a game of ${game.name} with a total of %1 cards! This is my %2 win!
}
{9}

#ACTION {^+MSG you have won the game, with a total of %d brains!$}
{
	#variable {game.message} {I won ${game.name} with a total of %1 brains!}
}
{9}

#ALIAS {^POST %1}
{
	#if {&{gs}}
	{
		#regexp {%1} {{.*}#RSGames{.*}} {#system {curl -s -u "${gs.user}:${gs.password}" --data-urlencode status="%1" -d source="tintin-rsgames" ${gs.url}/api/statuses/update.json &> /dev/null&}};
		#else
		{
			#system {curl -s -u "${gs.user}:${gs.password}" --data-urlencode status="%1 #RSGames" -d source="tintin-rsgames" ${gs.url}/api/statuses/update.json &> /dev/null&}
		};
		#echo {Posted to ${gs.url}!};
		#unvariable {game.message}
	}
}
{9}

#ALIAS {^ginfo %S@%S.%S %S$}
{
	#class {settings} {open};
	#variable {gs.user} {%1};
	#variable {gs.url} {https://%2.%3};
	#variable {gs.password} {%4};
	#class {settings} {write} {$HOME/config.tin};
	#echo {Now posting as ${gs.user} on ${gs.url}\.};
	#history {delete}
}
{9}

#ALIAS {^ginfo$}
{
	#class {settings} {open};
	#unvariable {gs};
	#class {settings} {write} {$HOME/config.tin};
	#echo {GNU Social settings cleared.}
}
{9}
